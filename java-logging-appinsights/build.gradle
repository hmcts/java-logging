plugins {
  id 'io.spring.dependency-management' version '1.0.9.RELEASE'
}

def versions = [
  springBoot: '2.3.0.RELEASE',
  appInsights: '2.5.1'
]

publishing {
  publications {
    Insights(MavenPublication) {
      from components.java
      artifact sourcesJar
      artifact javadocJar
      groupId project.group
      artifactId 'logging-appinsights'
      version project.version
    }
  }
}

bintray {
  user = System.getenv('BINTRAY_USER')
  key = System.getenv('BINTRAY_KEY')
  publications = ['Insights']
  publish = true
  pkg {
    repo = 'hmcts-maven'
    name = 'logging-appinsights'
    userOrg = 'hmcts'
    licenses = ['MIT']
    vcsUrl = 'https://github.com/hmcts/java-logging'
    version {
      name = project.version
    }
  }
}

repositories {
  flatDir {
    dirs "$projectDir/lib"
  }
}

dependencyManagement {
  imports {
    mavenBom "org.springframework.boot:spring-boot-dependencies:${versions.springBoot}"
  }
}

dependencies {
  compileOnly group: 'org.springframework.boot', name: 'spring-boot-autoconfigure'

  compile group: 'com.microsoft.azure', name: 'applicationinsights-logging-logback', version: versions.appInsights
  compile group: 'com.microsoft.azure', name: 'applicationinsights-spring-boot-starter', version: versions.appInsights

  testCompile group: 'org.springframework.boot', name: 'spring-boot-starter-test'
  testCompile group: 'org.springframework.boot', name: 'spring-boot-starter-web'

  testCompile group: 'org.assertj', name: 'assertj-core', version: '3.15.0'
}

task sourcesJar(type: Jar, dependsOn: classes) {
  classifier = 'sources'
  from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
  classifier = 'javadoc'
  from javadoc.destinationDir
}

artifacts {
  archives sourcesJar
  archives javadocJar
}
