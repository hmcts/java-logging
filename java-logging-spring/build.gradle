def versions = [
  spring: '1.5.9.RELEASE'
]

publishing {
  publications {
    Spring(MavenPublication) {
      from components.java
      artifact sourcesJar
      artifact javadocJar
      groupId project.group
      artifactId 'java-logging-spring'
      version project.version
    }
  }
}

bintray {
  user = System.getenv('BINTRAY_USER')
  key = System.getenv('BINTRAY_KEY')
  publications = ['Spring']
  publish = true
  pkg {
    repo = 'hmcts-maven'
    name = 'java-logging-spring'
    userOrg = 'hmcts'
    licenses = ['MIT']
    vcsUrl = 'https://github.com/hmcts/java-logging'
    version {
      name = project.version
    }
  }
}

dependencies {
  compile project(':')
  compileOnly group: 'org.springframework.boot', name: 'spring-boot-autoconfigure', version: versions.spring

  testCompile project(':').sourceSets.test.output
  testCompile group: 'org.springframework.boot', name: 'spring-boot-starter-web', version: versions.spring
  testCompile group: 'org.springframework.boot', name: 'spring-boot-starter-security', version: versions.spring
  testCompile group: 'org.springframework.boot', name: 'spring-boot-starter-test', version: versions.spring
  testCompile group: 'org.springframework.security', name: 'spring-security-test', version: '4.2.3.RELEASE'
  testCompile group: 'com.google.code.tempus-fugit', name: 'tempus-fugit', version: '1.1'
}

task sourcesJar(type: Jar, dependsOn: classes) {
  classifier = 'sources'
  from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
  classifier = 'javadoc'
  from javadoc.destinationDir
}

artifacts {
  archives sourcesJar
  archives javadocJar
}
