plugins {
  id 'org.owasp.dependencycheck' version '12.1.1'
  id 'com.github.ben-manes.versions' version '0.52.0'
}

def buildNumber = System.getenv("RELEASE_VERSION")?: "DEV-SNAPSHOT"

allprojects {
  group 'com.github.hmcts.java-logging'
  version buildNumber

  apply plugin: 'java'
  apply plugin: 'java-library'
  apply plugin: 'maven-publish'
  apply plugin: 'checkstyle'
  apply plugin: 'jacoco'
  apply plugin: 'com.github.ben-manes.versions'
  apply plugin: 'pmd'

  checkstyle {
    toolVersion = '10.23.1'
    maxWarnings = 0
    getConfigDirectory().set(new File(rootDir, "config/checkstyle"))
  }

  pmd {
    toolVersion = "7.13.0"
    ignoreFailures = true
    incrementalAnalysis = true
    sourceSets = [sourceSets.main, sourceSets.test]
    reportsDir = file("$rootProject.buildDir/reports/pmd/$project.name")
    ruleSetFiles = files("$rootProject.projectDir/config/pmd/ruleset.xml")
  }

  jacoco {
    toolVersion = "0.8.13"
  }

  java {
    toolchain {
      languageVersion = JavaLanguageVersion.of(17)
    }
  }

  repositories {
    mavenLocal()
    mavenCentral()
  }

  dependencies {
    testImplementation group: 'junit', name: 'junit', version: '4.13.2'
  }

  // before committing a change, make sure task still works
  dependencyUpdates {
    def isNonStable = { String version ->
      def stableKeyword = ['RELEASE', 'FINAL', 'GA'].any { qualifier -> version.toUpperCase().contains(qualifier) }
      def regex = /^[0-9,.v-]+$/
      return !stableKeyword && !(version ==~ regex)
    }
    rejectVersionIf { selection -> // <---- notice how the closure argument is named
      return isNonStable(selection.candidate.version) && !isNonStable(selection.currentVersion)
    }
  }

  dependencyCheck {
    // Specifies if the build should be failed if a CVSS score above a specified level is identified.
    // range of 0-10 fails the build, anything greater and it doesn't fail the build
    failBuildOnCVSS = System.getProperty('dependencyCheck.failBuild') == 'true' ? 0 : 11
    suppressionFile = "$rootDir/config/owasp/suppressions.xml"
    analyzers {
      // Disable scanning of .NET related binaries
      assemblyEnabled = false
    }
  }
}

task codeCoverageReport(type: JacocoReport, dependsOn: allprojects*.test) {

  // Gather execution data from all subprojects
  // (change this if you e.g. want to calculate unit test/integration test coverage separately)
  executionData fileTree(project.rootDir.absolutePath).include("**/build/jacoco/*.exec")

  // Add all relevant sourcesets from the subprojects
  allprojects.each {
    sourceSets it.sourceSets.main
  }

  reports {
    csv.required = false
    html.required = true
    html.destination = file("${buildDir}/reports/jacoco/html")
    xml.required = true
    xml.destination = file("${buildDir}/reports/jacoco/jacocoTestReport.xml")
  }
}

java {
  withJavadocJar()
  withSourcesJar()
}

publishing {
  publications {
    Main(MavenPublication) {
      from components.java
      groupId project.group
      artifactId 'logging'
      version project.version
    }
  }

  repositories {
    maven {
      name = "AzureArtifacts"
      url = uri("https://pkgs.dev.azure.com/hmcts/Artifacts/_packaging/hmcts-lib/maven/v1")
      credentials {
        username = System.getenv("AZURE_DEVOPS_ARTIFACT_USERNAME")
        password = System.getenv("AZURE_DEVOPS_ARTIFACT_TOKEN")
      }
    }
  }
}

dependencies {
  testImplementation group: 'org.mockito', name: 'mockito-core', version: '5.17.0'
  testImplementation group: 'org.assertj', name: 'assertj-core', version: '3.27.3'
  testImplementation group: 'com.github.stefanbirkner', name: 'system-rules', version: '1.19.0'
  testImplementation group: 'javax.servlet', name: 'javax.servlet-api', version: '4.0.1'
  testImplementation group: 'com.google.code.tempus-fugit', name: 'tempus-fugit', version: '1.1'
}
